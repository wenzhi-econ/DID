---
title: "Mixtape-Exercises-Violations of PT"
output: 
  html_document: default
  github_document: default
date: '2022-06-23'
---

```{r setup, include=FALSE}
knitr::opts_chunk$set(echo = TRUE, warning = F)
# devtools::install_github("Hemken/Statamarkdown")
library(Statamarkdown)
```

## Introduction


This exercise will walk you through using the HonestDiD R or Stata package to conduct sensitivity analysis for possible violations of parallel trends, using the methods proposed in [Rambachan and Roth (2022)](https://jonathandroth.github.io/assets/files/HonestParallelTrends_Main.pdf). Here are links to the [Stata package](https://github.com/mcaceresb/stata-honestdid) and [R package](https://github.com/asheshrambachan/HonestDiD).

## 0. Install packages if needed 

We will use several Stata packages in our analysis, which you can install as follows if needed. 

```{stata, eval=FALSE}
* reghdfe
ssc install reghdfe

* honestdid
net install honestdid, from("https://raw.githubusercontent.com/mcaceresb/stata-honestdid/main") replace
honestdid _plugin_check

* csdid 
net install csdid, from ("https://raw.githubusercontent.com/friosavila/csdid_drdid/main/code/") replace
```

## 1. Run the baseline DiD

For simplicity, we will first focus on assessing sensitivity to violations of parallel trends in a non-staggered DiD. Load the same dataset on Medicaid as in the previous exercise. Restrict the sample to the years 2015 and earlier, drop the small number of units who are first treated in 2015. We are now left with a panel dataset where some units are first treated in 2014 and the remaining units are not treated during the sample period.

```{stata load-data, collectcode = TRUE}
use "https://raw.githubusercontent.com/Mixtape-Sessions/Advanced-DID/main/Exercises/Data/ehec_data.dta", clear

* Keep years before 2016. Drop the 2016 cohort
keep if (year < 2016) & (missing(yexp2) | (yexp2 != 2015))

* Create a treatment dummy
gen D = (yexp2 == 2014)
gen Dyear = cond(D, year, 2013)

```

Start by running the simple TWFE regression $Y_{it} = \alpha_i + \lambda_t + \sum_{s \neq 2013} 1[s=t] \times D_i \times \beta_s + u_{it} ,$ where $D_i =1$ if a unit is first treated in 2014 and 0 otherwise. Note that since we do not have staggered treatment, the coefficients $\hat{\beta}_s$ are equivalent to DiD estimates between the treated and non-treated units between period $s$ and 2013. I recommend using the `feols` command from the `fixest` package in R and `reghdfe` command in Stata; although feel free to use your favorite regression command. Don't forget to cluster your SEs at the state level. 

```{stata run-twfe, collectcode = TRUE, echo = 1:4}
* Run the TWFE spec
reghdfe dins b2013.Dyear, absorb(stfips year) cluster(stfips) noconstant

coefplot, vertical yline(0) ciopts(recast(rcap)) xlabel(,angle(45)) ytitle("Estimate and 95% Conf. Int.") title("Effect on dins")
quietly graph export twfe.svg, replace
```

![TWFE Estimates](twfe.svg)


## 3. Sensitivity analysis using relative magnitudes restrictions

We are now ready to apply the HonestDiD package to do sensitivity analysis. Suppose we're interested in assessing the sensitivity of the estimate for 2014 (the first year of treatment). We will use the "relative magnitudes" restriction that allows the violation of parallel trends between 2013 and 2014 to be no more than $\bar{M}$ times larger than the worst pre-treatment violation of parallel trends. 

To create a sensitivity analysis, use the `honestdid` function. You will need to pass the options `pre` and `post` to specify the pre and post treatment estimates. I suggest that you also give the optional parameter `mvec` a value of `0.5(0.5)2` to specify the values of $\bar{M}$ you wish to use. (Note: it may take a couple of minutes to calculate the sensitivity results.)

```{stata sensitivity-relative-magnitudes}
honestdid, pre(1/5) post(7/8) mvec(0.5(0.5)2)
```

Look at the results of the sensitivity analysis you created. For each value of $\bar{M}$, it gives a robust confidence interval that allows for violations of parallel trends between 2013 and 2014 to be no more than $\bar{M}$ times the max pre-treatment violation of parallel trends. What is the "breakdown" value of $\bar{M}$ at which we can no longer reject a null effect? Interpret this parameter. 

## 4. Create a sensitivity analysis plot

We can also visualize the sensitivity analysis using the `honestdid` command by adding the `coefplot` option. You can use the `cached` option to use the results from the previous `honestdid` call (for speed's sake). 

```{stata sensitivity-relative-magnitudes-plot, echo = 1}
honestdid, pre(1/5) post(7/8) mvec(0.5(0.5)2) coefplot xtitle("M") ytitle("95% Robust CI")
quietly graph export sensitivity_1.svg, replace
```

```{r, echo = FALSE, fig.cap = "Sensitivity Analysis using Assumptions on the Relative Magnitude"}
knitr::include_graphics("sensitivity_1.svg")
```

## 5. Sensitivity Analysis Using Smoothness Bounds

We can also do a sensitivity analysis based on different restrictions on what violations of parallel trends might look like. The starting point for this analysis is that often if we're worried about violations of parallel trends, we let treated units be on a different time-trend relative to untreated units. Rambachan and Roth consider a sensitivity analysis based on this idea -- how much would the difference in trends need to differ from linearity to violate a particular result? Specifically, they introduce a parameter $M$ that says that the change in the slope of the trend can be no more than $M$ between consecutive periods. 

To create a sensitivity analysis using smoothness bounds, add the `delta(sd)` option to your `honestdid` function call. (Note: as before it may take a couple of minutes for the sensitivity code to run.) What is the breakdown value of $M$ -- that is, how non-linear would the difference in trends have to be for us not to reject a significant effect? 

```{stata, echo = 1}
honestdid, pre(1/5) post(6/7) mvec(0(0.01)0.05) delta(sd) omit coefplot xtitle("M") ytitle("95% Robust CI")
quietly graph export sensitivity_2.svg, replace
```

```{r, echo = FALSE, fig.cap = "Sensitivity Analysis using Assumptions on the Smoothness of Pre-trends"}
knitr::include_graphics("sensitivity_2.svg")
```

## 6. Bonus: Sensitivity Analysis for Average Effects

Re-run the sensitivity analyses above using the option `l_vec` to do sensitivity on the `average` effect between 2014 and 2015 rather than the effect for 2014. To do so, run the following `matrix l_vec = 0.5 \ 0.5` and then add `l_vec(l_vec)` to the `honestdid` call (`matrix l_vec = 0 \ 1` would give inference on the 2015 effect). How do the breakdown values of $\bar{M}$ and $M$ compare to those for the effect in 2014? [Hint: breakdown values for longer-run effects often tend to be smaller, since this leaves more time for the groups' trends to diverge from each other.] 

```{stata, echo = 1:3}
matrix l_vec = 0.5 \ 0.5
local plotopts xtitle(Mbar) ytitle(95% Robust CI)
honestdid, l_vec(l_vec) pre(1/5) post(6/7) mvec(0(0.5)2) omit coefplot xtitle("M") ytitle("95% Robust CI")
quietly graph export sensitivity_3.svg, replace
```

```{r, echo = FALSE, fig.cap = "Sensitivity Analysis on Average Effects"}
knitr::include_graphics("sensitivity_3.svg")
```


## 7. Bonus 2: HonestDiD + Callaway & Sant'Anna

Look at the instructions [here](https://github.com/mcaceresb/stata-honestdid#staggered-timing) for running an event-study using Callaway and Sant'Anna and passing the results to the HonestDiD package for sensitivity analysis. Create a Callaway and Sant'Anna event-study using the full Medicaid data, and then apply the HonestDiD sensitivity. [Hint: I recommend using `window(-4 5)` in the `csdid_estat` command, since the earlier pre-trends coefficients are very noisy.]


```{stata, echo = 1:9}
use "https://raw.githubusercontent.com/Mixtape-Sessions/Advanced-DID/main/Exercises/Data/ehec_data.dta", clear
qui sum year, meanonly
replace yexp2 = cond(mi(yexp2), r(max) + 1, yexp2)
qui csdid dins, time(year) ivar(stfips) gvar(yexp2) long2 notyet
csdid_estat event, window(-4 5) estore(csdid)
estimates restore csdid

local plotopts xtitle(Mbar) ytitle(95% Robust CI)
honestdid, pre(3/6) post(7/12) mvec(0.5(0.5)2) coefplot xtitle(Mbar) ytitle(95% Robust CI)
quietly graph export sensitivity_4.svg, replace
```

```{r, echo = FALSE, , fig.cap = "Sensitivity Analysis using Callaway and Sant'Anna Estimator"}
knitr::include_graphics("sensitivity_4.svg")
```
